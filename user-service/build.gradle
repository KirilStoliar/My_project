plugins {
    id 'org.sonarqube' version '4.4.1.3373'
    id 'jacoco'
}

springBoot {
    buildInfo()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Database
    runtimeOnly 'org.postgresql:postgresql'

    // Liquibase
    implementation 'org.liquibase:liquibase-core'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Auditing
    implementation 'org.springframework.data:spring-data-envers'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // Добавляем Spring Security и JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Тестирование
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // TestContainers
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    // Metrics
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.opentelemetry:opentelemetry-api:1.39.0'

    // Loki
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

tasks.register('integration-test', Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    systemProperty "spring.profiles.active", "integration-test"
    include '**/*IntegrationTest*'
    include '**/*RepositoryTest*'

    dependsOn testClasses
    mustRunAfter test

    maxParallelForks = 1
    forkEvery = 1

    timeout = Duration.ofMinutes(10)

    // Требует Docker
    doFirst {
        println "Running integration tests with Docker..."
        println "Make sure Docker is running!"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperty "spring.profiles.active", "test"

    exclude '**/*IntegrationTest*'
    exclude '**/*RepositoryTest*'
}

tasks.register('allTests') {
    dependsOn test, integrationTest
    doLast {
        println "All tests completed!"
    }
}

// JaCoCo для покрытия кода
jacoco {
    toolVersion = "0.8.10"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/entity/**',
                    '**/dto/**',
                    '**/config/**',
                    '**/exception/**',
                    '**/response/**',
                    '**/UserServiceApplication.class'
            ])
        }))
    }
}

sonar {
    properties {
        property "sonar.projectKey", "KirilStoliar_Innowise_trainee_spring"
        property "sonar.projectName", "User Service"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "kirilstoliar"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.junit.reportPaths", "build/test-results/test"
        property "sonar.coverage.exclusions", "**/entity/**,**/dto/**,**/config/**,**/exception/**,**/response/**,**/UserServiceApplication.java"
        property "sonar.gradle.skipCompile", "true"
        property "sonar.scm.disabled", "true"
        property "sonar.analysis.detectedci", "github-actions"
    }
}